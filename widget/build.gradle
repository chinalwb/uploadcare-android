plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id "androidx.navigation.safeargs.kotlin"
    id 'maven-publish'
    id 'com.jfrog.bintray'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion versions.compile_sdk

    defaultConfig {
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.target_sdk
        versionCode 12
        versionName "3.1.1"
    }

    buildFeatures {
        dataBinding true
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix '-DEBUG'
            buildConfigField "String", "SOCIAL_API_ENDPOINT", '"https://social.uploadcare.com/"'
        }
        release {
            debuggable false
            minifyEnabled false
            buildConfigField "String", "SOCIAL_API_ENDPOINT", '"https://social.uploadcare.com/"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    dokkaHtml.configure {
        dokkaSourceSets {
            named("main") {
                outputDirectory.set(buildDir.resolve("javadoc"))
                sourceRoots.setFrom(file("src/main/java"))
                noAndroidSdkLink.set(false)
                skipEmptyPackages.set(true)
                includeNonPublic.set(false)
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.uploadcare.android.widget'
                artifactId = 'uploadcare-android-widget'
                version = android.defaultConfig.versionName

                // Define this explicitly if using implementation or api configurations
                pom.withXml {
                    def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    user = properties.containsKey('bintray.user') ? properties.getProperty("bintray.user") : ""
    key = properties.containsKey('bintray.key') ? properties.getProperty("bintray.key") : ""

    publications = ['release']
    dryRun = true
    publish = true

    pkg {
        repo = 'maven'
        name = 'uploadcare-android-widget'
        licenses = ['Apache-2.0']
        desc = 'Android client widget library for the Uploadcare API.'
        websiteUrl = 'https://github.com/uploadcare/uploadcare-android'
        issueTrackerUrl = 'https://github.com/uploadcare/uploadcare-android/issues'
        vcsUrl = 'https://github.com/uploadcare/uploadcare-android.git'

        version {
            name = android.defaultConfig.versionName
        }
    }
}

dependencies {
    implementation project (':library')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlin_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlin_coroutines"

    implementation "androidx.core:core-ktx:$versions.core"

    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    implementation "androidx.fragment:fragment-ktx:$versions.fragment"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"

    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    implementation "androidx.work:work-runtime-ktx:$versions.work"
    implementation "androidx.work:work-gcm:$versions.work"

    implementation "com.squareup.moshi:moshi:$versions.moshi"
    implementation "com.squareup.moshi:moshi-adapters:$versions.moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshi"

    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"

    implementation "com.squareup.picasso:picasso:$versions.picasso"

    androidTestImplementation "androidx.test:runner:$versions.test_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso_core"
}
